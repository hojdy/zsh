#!/bin/zsh
setopt localoptions extendedglob
local curcontext=$curcontext; [[ -z $curcontext ]] &&
  curcontext=$WIDGET:::

_main_complete

compstate[insert]="${${(M)WIDGET:#menu-*}:+menu:}"

if zstyle -t :autocomplete:tab: insert-unambiguous && [[ $KEYS == $key[Tab] ]] &&
    (( _lastcomp[nmatches] > 1 && $#_lastcomp[unambiguous] > $#words[CURRENT] )); then
  compstate[insert]+='0'
  return 0
fi

case $KEYS in
  $key[BackTab])
    compstate[insert]+='0'
    ;;
  *)
    compstate[insert]+='1'
    ;|
  $key[Tab])
    if (( _lastcomp[nmatches] == 1 || CURRENT == 1 )) || [[ $compstate[context] == command ]]; then
      compstate[insert]+=' '
    fi
    ;;
esac
(( compstate[nmatches] > 0 ))
